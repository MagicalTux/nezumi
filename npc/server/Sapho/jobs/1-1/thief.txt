//===== eAthena Script =======================================
//= Thief Job Quest
//===== By: ==================================================
//= eAthena dev team
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= eAthena 1.0 +
//===== Description: =========================================
//= Traduit par GALion
//===== Additional Comments: =================================
//= Fully working
//= v1.1 Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= Fixed possible EXP abuse [Lupus]
//============================================================


// == Monsters ==
//Spawn is included in this file so make shure its not elsewhere to!
job_thief1.gat,0,0,0,0	monster	Orange Mushroom	1182,180,0,0,0
job_thief1.gat,0,0,0,0	monster	Fabre	1184,50,0,0,0
job_thief1.gat,0,0,0,0	monster	Chonchon	1183,50,0,0,0
job_thief1.gat,0,0,0,0	monster	Spore	1014,30,0,0,0


// == NPCs ==
// -- Interviewer --
moc_prydb1.gat,39,129,5	script	Guide des Thief	69,{
	callfunc "Thi_check";
	if(job_thief_q==2) goto L_1;
	if(job_thief_q==1) goto L_Back;

	mes "[Thief Guide]";
	if (Sex) goto mec;
	mes "Hey... Qu'est-ce qu'une fille comme vous vient faire dans ce trou à rat?";
	goto suite;
mec:
	mes "Hey...Qu'est-ce qu'un type comme vous vient faire dans ce trou à rat?";
suite:
	next;
M_Menu:
	menu "Je ne suis pas venu(e) pour l'ambiance...",M_0,"Moi? Je me promène c'est tout...",M_End,"Je cherche une vérité",M_Truth;

		M_0:
		mes "[Guide des Thief]";
		mes "Hmph... Vous avez l'air un peu insolent... Vous savez, être un voleur ce n'est pas seulement se la péter...";
		mes "Ca me rappelle... Je devais être niveau 9 ou 10... C'était mon premier vol...";
		next;
		mes "[Guide des Thief]";
		mes "Hahahaha... Je revois encore la tronche du mec...";
		emotion e_heh;
		next;
		mes "[Guide des Thief]";
		mes "Bref... Je suppose que vous êtes là pour devenir thief...";
		next;
		menu "-Vous avez tout pigé.",sM_0a, "-Nan. Juste pour gaspiller votre temps ^ ^",sM_0b, "-Pourquoi vous avez volé de type?",sM_0c;

		sM_0a:
			goto L_Test;
		sM_0b:
			mes "[Guide des Thief]";
			mes "Whaa... Merciiii... Maintenant CASSEZ-VOUS DE LA!!";
			emotion e_an;
			close;
		sM_0c:
			mes "[Guide des Thief]";
			mes "Hein? Moi?... Et bien... Je n'avais pas vraiment d'autre choix à ce moment... C'était je vole ou je crève de faim.";
			mes "J' pouvais pas soufrir de ça un jour de plus.";
			close;

	M_Truth:
		mes "[Guide des Thief]";
		mes "Pour la voir éclater, vous devez être sous l'emprise magique d'un livre où la foi est omniprésente.";
		close;

	M_End:
		mes "[Guide des Thief]";
		mes "Et vous devez continuer à gaspiller mon temps!!?";
		emotion e_pif;
		close;

	L_Test:
		mes "[Guide des Thief]";
		mes "Ok. Remplissez ce formulaire.";
		next;
		mes "(vous remplissez le questionnaire et le lui rendez)";
		next;
		if(JobLevel < 10) goto sL_JobLvl;

		mes "[Guide des Thief]";
		mes "Alors, ^ff0000"+ strcharinfo(0) + "^000000, si c'est vraiment ton nom... Vous avez fait des exploits.";
		mes "Voyons ça... Attaque aggravée... crimes... mmhmm...";
		next;
		mes "[Guide des Thief]";
		mes "Ah, on y est... Vous avez le niveau de job et de compétences (skills) pour être un Thief. Maintenant, vous devez juste passer le test de Thief de Morocc.";
		next;

		L_Explain:
		mes "[Guide des Thief]";
		mes "Le test exige que vous vous ^ff0000'faufilliez'^000000 dans ^ff0000'la ferme de Shibu'^000000, et que vous ^ff0000'voliez'^000000 quelques ^aaaa00'champignons'^000000.";
		mes "Il y a ^ff00002^000000 sortes de ^aaaa00champignons^000000 dans la ferme, ^ffbb00'"+getitemname(1069)+"'^000000 et ^ffbb00'"+getitemname(1070)+"'^000000.";
		next;
		mes "[Guide des Thief]";
		mes "'Shibu, c'est juste le pire escroc de tous les Marchands de Morroc, au cas où vous vous demanderiez pourquoi c'est notre cible.";
		next;
		mes "[Guide des Thief]";
		mes "Rapportez les champignons ici, et on vous notera en fonction du type et de la quantité de champignons que vous ramènerez.";
		mes "1 '"+getitemname(1069)+"' = ^0000ff3 points^000000 tandis que 1 '"+getitemname(1070)+"' = ^0000ff1 point^000000. Pour résussir le test, il faut 25 points.";
		next;
		mes "[Guide des Thief]";
		mes "Pour aller dans la ferme de Shibu, il vous faudra parler à un de nos 'associés'. Vous le trouverez à l'entrée de la grande pyramide près des colones.";
		mes "On l'appelle ^ddcc00'irrevelant man'^000000.";
		next;
		mes "[Guide des Thief]";
		mes "Voici ses coordonnées exactes: '^FF0000141, 125^000000'.";
		mes "Trouve-le et il vous conduiras jusqu'à la ferme.";
		next;
		mes "[Guide des Thief]";
		mes "Ca a l'air facile comme ca mais ^009900vous savez, il y a des monstres qui gardent la culture de champignons^000000?";				//had to leave the engrish in, it was to good not too.  d^_^b
		mes "Alors ^0000ffarrêtez de vous imaginer des Contes de fées^000000 et ^ff0000'bougez-vous un peu. Ca va être votre expérience la plus difficile^000000.";
		next;
		mes "[Guide des Thief]";
		mes "Puis, je vais vous donner un truc. ^ff0000Elaborez une stratégie^000000 avant de rentrer dans la ferme.";
		next;
		mes "[Guide des Thief]";
		mes "Essayez de voler les champignons aussi vite que possible. Vous devrez peut-être ^0000ffenlever des armes et votre armure^000000.";
		mes "Vous ne voulez pas être tué pendant ce test. Je voudrais au moins que vous me ramèniez quelques champignons d'abord... Ensuite, vous pourrez vous faire tuer... HA!";
		next;
		mes "[Guide des Thief]";
		mes "Si vous n'avez pas de questions... BOUGEZ!";
		emotion e_go;
		set job_thief_q,1;
		close;

		sL_JobLvl:
			mes "[Guide des Thief]";
			mes "Je vois... Vous êtes ambitieux… Mais avant de mettre une arme dans la main d'un morveux, vous devez avoir appris les 9 'compétences (skills) de base'.";
			close;

	L_Back:
		if(countitem(1069)>0 || countitem(1070)>0) goto sL_1;//Items: Orange_Net_Mushroom, Orange_Gooey_Mushroom_,

		sL_0:
			mes "[Guide des Thief]";
			mes "... Qu'est-ce que vous faîtes ici!! Vous devriez ramasser des champignons, vous vous souvenez??";
			mes "Ne me dîtes pas que vous ne savez plus ce que vous avez à faire??? Clanpin... Vous voulez que je vous ré-explique?...";
			emotion e_hmm;
			next;
			menu "Hey ouais...",sM_1a, "Non",sM_1b;

			sM_1a:
				mes "[Guide des Thief]";
				mes "(soupir)Y a toujours quelqu'un qui se gourre... C'est la dernière fois, alors ECOUTE BIEN!!....";
				emotion e_pif;
				next;
				goto L_Explain;
			sM_1b:
				mes "[Guide des Thief]";
				mes "... Ben alors c'est quoi le problème?? Vous voulez me dire quelque chose? VOUS êtes CELUI qui est venu à MOI...";
				mes "Quoi!!!!... Vous voulez ma photo?? HEIN!!??";
				emotion e_ag;
				close;

		sL_1:
			mes "[Guide des Thief]";
			mes "Quoi? Vous avez réellement volé des champignons?? Vous ne seriez pas un peu poulpe?";
			mes "Haha... Je crois que vous ne m'avez pas écouté...";
			emotion e_what;
			next;
			mes "[Guide des Thief]";
			mes "... Naaaan!! Je rigole... heheheh! Allez vois le type à coté pour noter vos champignons.";
			emotion e_heh;
			close;

	L_1:
		mes "[Guide des Thief]";
		mes "C'était comment la ferme? Vous vous êtes bien marré(e)?";
		next;
		menu "Yeah, super.",M_Cool,"C'était horrible.",M_Not;

		M_Cool:
			mes "[Guide des Thief]";
			mes "Ouais! Je vous aime bien! Beaucoup de candidats sont des pétochards, vous savez.";
			next;
			mes "[Guide des Thief]";
			mes "... Vous n'allez pas me piquer ma place, si? Si c'est ça que vous voulez, je vais vous ^ff0000'botter le fion'^000000!";	//this is just too much lol.
			emotion e_gasp;
			next;
			mes "[Guide des Thief]";
			mes " Pour finir le test, il vous faut quelques champignons et aller voir Brad.";
			close;
		M_Not:
			mes "[Guide des Thief]";
			mes "Je sais ce que vous pensez. J'y suis allé aussi et c'était terrible. Tous ces champignons puants et ces monstres aggressifs.";
			next;
			mes "[Guide des Thief]";
			mes "Pour finir le test, il vous faut quelques champignons et aller voir Brad.";
			emotion e_wah;
			close;
}


// -- Test Grader --
moc_prydb1.gat,42,133,2	script	Brad	118,{
	callfunc "F_ToHigh",Job_Thief_High,"Thief High",Job_Assassin_Cross,"Assassin Cross",149,150,151,152,"Brad";
	if(BaseJob == 0) goto L_Novice;
	if(BaseJob == 6) goto L_Thief;

L_Other:
	mes "[Brad]";
	mes "Hey~ Hey~!! Vous n'avez pas l'air d'un Thief. Que diable? Vous ne devriez rien tenter sur le TERRITOIRE DES THIEFS!!";
	emotion e_omg;
	close;
L_Thief:
	mes "[Brad]";
	mes "On a rien de spécial pour les voleurs actuellement. Reviennez une autre fois, ok?";
	close;

L_Novice:
	mes "[Brad]";
	if((job_thief_q == 2) && ((countitem(1069) > 0) || (countitem(1070) > 0))) goto L_4;//Items: Orange_Net_Mushroom, Orange_Gooey_Mushroom_,
	if(job_thief_q >= 1) goto L_3;

	mes "Hey? Qu'y a-t-il petit(e)? Si vous voulez devenir thief, allez dans la file d'à coté.";
	close;
L_3:
	mes "Allez chercher des champignons et je pourrais peut-être quelque chose pour vous.";
	close;
L_4:
	mes "Bien. Vous avez piqué des champignons à ce maudit Shibu...";
	next;

	set @mushrm1,countitem(1069)*3;//Items: Orange_Net_Mushroom,
	set @mushrm2,countitem(1070);//Items: Orange_Gooey_Mushroom_,
	set @TotMush,@mushrm1 + @mushrm2;
	set @money_thief,((countitem(1069) * 5) + (countitem(1070)* 2)) + 200;//Items: Orange_Net_Mushroom, Orange_Gooey_Mushroom_,

	mes "[Brad]";
	mes "Tu as:";
	mes "^0000ff"+countitem(1069)+"^000000 '"+getitemname(1069)+"' pour ^ffbb00"+@mushrm1+"^000000 points,";//Items: Orange_Net_Mushroom,
	mes "^0000ff"+countitem(1070)+"^000000 '"+getitemname(1070)+"' pour ^ffbb00"+@mushrm2+"^000000 points,";//Items: Orange_Gooey_Mushroom_,
	mes "Ce qui fait un total de  ^ff0000"+@TotMush+"^000000.";
	next;
	if (@TotMush > 25) goto L_High;
	if (@TotMush == 25) goto L_Medium;

		mes "[Brad]";
		mes "pfff!... Vous avez raté... Reviennez! Vous pouvez mieux faire! Encore quelques champignons, et ce sera bon!!!";
		close;

	L_Medium:
		mes "[Brad]";
		mes "Bien, vous avez réussi.";
		next;
		goto L_Final;

	L_High:
		mes "[Brad]";
		mes "Ooooh. Plus que 25. VOUS avez bien réussi!!";
		next;

	L_Final:
		mes "[Brad]";
		mes "Félicitations ^ff0000"+strcharinfo(0)+"^000000, vous avez officiellement réussi le test de Thief! Maintenant vous êtes Thief!!";
		emotion e_no1;
		next;
		if(SkillPoint > 0) goto sL_SkPoint;
		delitem 1069, countitem(1069);//Items: Orange_Net_Mushroom,
		delitem 1070, countitem(1070);//Items: Orange_Gooey_Mushroom_,

		callfunc "Job_Change",Job_Thief;
		callfunc "F_ClearJobVar";		// clears all job variables for the current player

		mes "[Brad]";
		mes "Voici une petite récompense pour votre dur labeur. Depensez la comme bon vous semble.";
		next;
		set Zeny,Zeny+@money_thief;
		mes "[Brad]";
		mes "A partir de maintenant, vous devez agir, penser et même puer comme un Thief.";
		mes "Vous êtes l'un(e) des nôtres maintenant, ce qui signifie que vous avez la confiance et l'amitié de tous les Thiefs dans tout Rune Midgard";
		next;
		mes "[Brad]";
		mes "Toutefois, si vous apportez le déshonneur à notre guilde, ou pire si vous nous trahissez, nous n'hésiterons pas à...";
		mes "Comment dire... je dirais... vous ^ff0000T'enlever'^000000 de la guilde des Thief.";
		emotion e_gg;
		next;
		mes "[Brad]";
		mes "Bonne chance et souviennez-vous de ^0000cc'notre mot d'ordre'^000000: ^0000ff NE PAS SE FAIRE ATTRAPER^000000l!";
		close;

		sL_SkPoint:
			mes "[Brad]";
			mes "Mais avant que cela arrive, vous devez utiliser tous vos points de compétence (skill), ok?";
			close;

}


// -- Irrelevant Man --
moc_ruins.gat,141,125,3	script	irrevelant man	118,{
	if(BaseJob == 6) goto L_Thief;
	if(BaseJob == 0) goto L_Novice;

L_Other:
	if(@s_flag==1) goto L_1b;
	if(@s_flag==2) goto L_1c;
	if(@s_flag==3) goto L_1d;
	if(@s_flag==4) goto L_1e;
	set @s_flag, 0;

	L_1a:
		mes "[Irrelevant Man]";
		mes "Lalala lala la. Quel jour merveilleux aujourd'hui?";
		mes "Je sens que je vais aller faire un 'Picnic' dans les pyramides avec un couple de d'amis.";
		set @s_flag, @s_flag+1;
		close;
	L_1b:
		mes "[irrevelant man]";
		mes "'Lalalala, foyer doux foyer.'";
		mes "J'aime cette phrase. Foyer doux foyer...";
		set @s_flag, @s_flag+1;
		close;
	L_1c:
		mes "[irrevelant man]";
		mes "... Je pourrais voler si j'étais plus petit...";
		set @s_flag, @s_flag+1;
		close;
	L_1d:
		mes "[irrevelant man]";
		mes "... Hmm?...";
		next;
		mes "[Irrelevent Man]";
		mes "Hey!! Ne me REGARDEZ pas comme ça! JE NE SUIS PAS CINGLE aujourd'hui, okay!!!";
		set @s_flag, @s_flag+1;
		emotion e_an;
		close;
	L_1e:
		mes "[irrevelant man]";
		mes "Hey! Je n'ai rien à vous dire! Occupez-vous de vos affaire! Tssss!!";
		emotion e_pif;
		close;

L_Thief:
	mes "[irrevelant man]";
	mes "Yah Hoo! Regardez-vous! Vous êtes devenu(e) Thief!";
	emotion e_no1;
	next;
	mes "[irrevelant man]";
	mes "Ne vous inquiétez pas pour la ferme de Shibu. Laissez les novices faire. Pourquoi ne partiriez-vous pas massacrer des monstres plus forts?";
	close;

L_Novice:
	if(job_thief_q==2) goto L_3;
	if(job_thief_q==1) goto L_2;
	mes "[irrevelant man]";
	mes "Hey!! Un(e) novice! Vous voulez devenir plus fort(e) et plus puissant(e)!? Vous voulez jouer à cache-cache?";
	mes "Si c'est que vous voulez, rejoignez la guide des Thief! Vous y serez bienvenu(e)! Rejoignez-la!";
	next;
	mes "[irrevelant man]";
	mes " Vous obtiendrez d'avantage d'informations, au premier sous-sol de la pyramide!";
	close;
L_2:
	mes "[irrevelant man]";
	mes "Pssst... Pssst... hey vous! Vous voulez passer le ^ff0000'Test'^000000, si je ne me trompe pas?";
	next;
	mes "[irrevelant man]";
	mes "Voyons-ça...( vérifie sa liste)... vous êtes ^ff0000"+ strcharinfo(0) +"^000000, non ? Bien.";
	next;
	mes "[irrevelant man]";
	mes "Ok! Je vous montre le chemin, mais CHUT. Je ne peux pas garantir votre sécurité, alors faîtes gaffe.";
	next;

	L_Warp:
	set @TEMP,rand(5);
	set job_thief_q,2;
	if(@TEMP != 0) goto warpL03a;
		warp "job_thief1.gat",228,106;
		close;
	warpL03a:
		if(@TEMP != 1) goto warpL03b;
		warp "job_thief1.gat",38,50;
		close;
	warpL03b:
		if(@TEMP != 2) goto warpL03c;
		warp "job_thief1.gat",66,331;
		close;
	warpL03c:
		if(@TEMP != 3) goto warpL03d;
		warp "job_thief1.gat",196,331;
		close;
	warpL03d:
		warp "job_thief1.gat",309,234;
		close;

L_3:
	mes "[irrevelant man]";
	mes "Muhahahaha~~ QUOI???~~ Vous n'avez pas ENCORE réussi le test? Vous tenez du poulpe!! hahahaha!!";
	emotion e_heh;
	next;
	mes "[irrevelant man]";
	mes "Je ne plaisante... PAS!!. On y retourne!";
	next;
	menu "Pret(e)",M_Yes,"pas encore",M_No;

	M_Yes:
		goto L_Warp;

	M_No:
		mes "[irrevelant man]";
		mes "Ok, faîtes-moi signe quand vous serez pret(e).";
		close;

}

//==============================================================================
// mapflag
//==============================================================================
job_thief1.gat	mapflag	nomemo
job_thief1.gat	mapflag	noteleport
job_thief1.gat	mapflag	nosave	SavePoint
job_thief1.gat	mapflag	nopenalty
job_thief1.gat	mapflag	nobranch
job_thief1.gat	mapflag	noexp
