//===== Script Athena =================================================//
//= Script 5-Etape_Finale.txt                                          //
//===== Par: ==========================================================//
//= Kylvan                                                             //
//= Kylvan@laposte.net                                                 //
//= Although all this quest is in French, you can mail me in English,  //
//= would be able to answer you.                                       //
//===== Version Actuelle: =============================================//
//= 1.0                                                                //
//===== Compatible Avec: ==============================================//
//= Freya 2.0.2 and newer.                                             //
//===== Description: ==================================================//
//  Il s'agit du NPC qui warpera le joueur a Valkyrie, afin de lui     //
//  permettre de renaitre. Les High Classe 1 devront lui parler        //
//  pour devenir High Classe 2.                                        //
//=====================================================================//

yggdrasil01.gat,253,138,5	script	Un Sage	803,{

	if ((readparam(19)>4000)&&(readparam(19)<4022)) goto L_Valky;
	if (sage == 1) goto L_Sage1;
	if (sage == 2) goto L_Sage2;
	if (sage == 3) goto L_Sage3;
	if ((sage == 4)||(sage==5)) goto L_Final;
	mes "[Un Sage]";
	mes "En la ville de Yuno, le sceau de votre ignorance pourra être brise";
	close;

L_Valky:
	mes "[Un Sage]";
	mes "Je suis heureux que vous soyez parvenu à nous rejoindre.";
	next;
	mes "[Un Sage]";
	mes "Désirez-vous vous rendre au Sanctuaire ?";
	menu "Oui",-,"Non",L_non;
	// mettre tous les flag qu'il faut ;p
	setmapflagnosave "valkyrie.gat","yggdrasil01.gat",0,0;
	setmapflag "valkyrie.gat",0; // no memo
	//setmapflag "valkyrie.gat",2; // no save
	setmapflag "valkyrie.gat",13; // no warp
	warp "valkyrie.gat",48,9;
	close;

L_Sage1:
	mes "[Un Sage]";
	mes "Le sceau de votre Ignorance a été brisé. Il vous faut purifier votre Esprit.";
	close;

L_Sage2:
	mes "[Un Sage]";
	mes "Votre Esprit est lave de toute souillure, il vous faut maintenant purifier votre Corps !";
	close;

L_Sage3:
	mes "[Un Sage]";
	mes "Votre Esprit et votre Corps sont lavés de toute souillure, il vous faut maintenant renforcer votre Volonte !";
	close;

L_Final:
	mes "[Un Sage]";
	mes "Vous avez accomplis tous les rituels.";
	next;
	mes "[Un Sage]";
	mes "En echange de toutes les connaissances que vous avez glané durant votre vie, les Dieux vous accorderons l'ultime present qui vous permettra de nous rejoindre dans la lumiere de la connaissance.";
	next;
	mes "[Un Sage]";
	mes "Ce present est la Renaissance !";
	next;
	mes "[Un Sage]";
	mes "Vous pourrez voir le monde tel qu'il est vraiment ! Et ainsi, vous ne pourrez que mieux le guider sous Leur divine protection.";
	next;
	mes "[Un Sage]";
	mes "Acceptez-vous ce marche ?";
	menu "Oui.",L_oui,"Non.",L_non;
L_oui:
	next;
	mes "[Un Sage]";
	mes "N'oubliez pas, vous devez ramener les peuples sous Leur divine protection.";
	next;
	mes "";//sinon on voit pas le message pecedent ~
	set sage,5;
	set jour5,gettime(5);
	set mois5,gettime(6);
	set an5,gettime(7);
	set heure5,gettime(3);
	set min5,gettime(2);
	set sec5,gettime(1);

	// mettre tous les flag qu'il faut ;p
	setmapflagnosave "valkyrie.gat","yggdrasil01.gat",0,0;
	setmapflag "valkyrie.gat",0; // no memo
	//setmapflag "valkyrie.gat",2; // no save
	setmapflag "valkyrie.gat",13; // no warp
	warp "valkyrie.gat",48,9;
	close;
L_non:
	mes "Puissiez-vous un jour changer d'avis.";
	close;
}

yggdrasil01.gat,191,187,5	duplicate(Un Sage)	Un Sage#2	803
yggdrasil01.gat,135,55,5	duplicate(Un Sage)	Un Sage#3	803
yggdrasil01.gat,271,234,5	duplicate(Un Sage)	Un Sage#4	803
yggdrasil01.gat,55,119,5	duplicate(Un Sage)	Un Sage#5	803


yggdrasil01.gat,1,1,0	script	deplac4	-1,{
OnInit:
	initnpctimer;
	end;

OnTimer1:
	disablenpc "Un Sage";
	disablenpc "Un Sage#2";
	disablenpc "Un Sage#3";
	disablenpc "Un Sage#4";
	disablenpc "Un Sage#5";

	set $@hazard4,rand(1,5);
	if ($@hazard4==1) enablenpc "Un Sage";
	if ($@hazard4==2) enablenpc "Un Sage#2";
	if ($@hazard4==3) enablenpc "Un Sage#3";
	if ($@hazard4==4) enablenpc "Un Sage#4";
	if ($@hazard4==5) enablenpc "Un Sage#5";
	end;
OnTimer3600000:
	setnpctimer 0;
	end;
}
