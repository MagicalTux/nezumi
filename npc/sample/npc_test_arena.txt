// ------------------------------------------------------------------
// アリ−ナの設定
// ------------------------------------------------------------------

// 次のマップを使います
//map: prontera.gat
//map: prt_are_in.gat
//map: force_1-1.gat

// テレポなどの禁止処理
prt_are_in.gat	mapflag	nomemo	dummy
prt_are_in.gat	mapflag	noteleport	dummy
prt_are_in.gat	mapflag	nosave	prontera.gat,156,191
force_1-1.gat	mapflag	nomemo	dummy
force_1-1.gat	mapflag	noteleport	dummy
force_1-1.gat	mapflag	nosave	prontera.gat,156,191

// プロンテラに入り口を置く
prontera.gat,160,185,0	script	アリーナ入り口	116,{
	mes "アリーナ入り口に飛びますか？";
	next;
	menu "飛ぶ",L_GOARENA,"やめる",L_YAME;
L_GOARENA:
	warp "prt_are_in.gat",31,82;
	close;
L_YAME:
	close;
}

// 待機室から準備室への転送
prt_are_in.gat,29,79,0	script	タイムアタック１	116,{
	if( getmapusers("force_1-1.gat")>0 ) goto L_WAIT;
	mes "ここはタイムアタックLv1です";
	mes "挑戦しますか？";
	menu "はい",L_GOLV1,"やめる",L_YAME;
L_GOLV1:
	if( getmapusers("force_1-1.gat")>0 ) goto L_WAIT;
	disablenpc "fc103-1";
	disablenpc "fc105";
	disablenpc "fc107";
	addtimer 5000,"arenatestev0000";
	warp "force_1-1.gat",99,12;
	end;
L_WAIT:
	mes "現在挑戦中の方がいますので、";
	mes "しばらくお待ちください。";
L_YAME:
	close;
}

// 準備室（１０秒準備期間）
force_1-1.gat,99,12,0	script	arenatestev0000	-1,{
	announce "１０秒後に開始します",3;
	addtimer 10000,"arenatestev0001";
}

// 実際の部屋への転送とMOBの設定
force_1-1.gat,99,12,0	script	arenatestev0001	-1,{
	killmonster "force_1-1.gat","arenatestev1000";
	monster "force_1-1.gat",25,25,"--ja--",1002,1,"arenatestev1000";
	monster "force_1-1.gat",20,25,"--ja--",1002,1,"arenatestev1000";
	monster "force_1-1.gat",25,20,"--ja--",1002,1,"arenatestev1000";
	monster "force_1-1.gat",30,25,"--ja--",1002,1,"arenatestev1000";
	monster "force_1-1.gat",25,30,"--ja--",1002,1,"arenatestev1000";
	set $arenatest00,5;
	disablenpc "fc101";
	disablenpc "fc103";
	warp "force_1-1.gat",25,26;
	enablenpc "fc103-1";
	enablenpc "fc105";
	enablenpc "fc107";
	announce "30秒以内に殲滅せよ",19;
	addtimer 30000,"arenatestev8000";
}

// 倒した処理
force_1-1.gat,25,26,0	script	arenatestev1000	-1,{
	set $arenatest00, $arenatest00 - 1;
	if( $arenatest00 > 0 ) goto L_CONT;
	deltimer "arenatestev8000";
	announce "クラッシュ!!",3;
	enablenpc "fc101";
	enablenpc "fc103";
	areaannounce "prt_are_in.gat",25,75,40,90,
		strcharinfo(0) + " がタイムアタックLv1をクリアしました",0;
	addtimer 5000,"arenatestev9000";
L_CONT:
	end;
}

// タイムアウト
force_1-1.gat,25,26,0	script	arenatestev8000	-1,{
	set $arenatest00,99;
	killmonster "force_1-1.gat","arenatestev1000";
	announce "タイムオーバー!!",3;
	areaannounce "prt_are_in.gat",25,75,40,90,
		strcharinfo(0) + " がタイムアタックLv1に失敗しました",0;
	addtimer 5000,"arenatestev9000";
}

// プロンテラに帰る
force_1-1.gat,25,26,0	script	arenatestev9000	-1,{
	warp "prontera.gat",156,191;
}
